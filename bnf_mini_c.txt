1.<Function> ::= <Type> identifier ( <ArgList> ) <CompoundStmt>

2.<ArgList> ::= <Arg> 
3.<ArgList> ::= <ArgList> , <Arg>

4.<Arg> ::= <Type> identifier

5.<Declaration> ::= <Type> <IdentList> ;

6.<Type> ::= int
7.<Type> ::= float

8.<IdentList> ::= identifier , <IdentList>
9.<IdentList> ::= identifier

10.<Stmt> ::= <ForStmt> 
11.<Stmt> ::= <WhileStmt> 
12.<Stmt> ::= <Expr> ; 
13.<Stmt> ::= <IfStmt> 
14.<Stmt> ::= <CompoundStmt>
15.<Stmt> ::= <Declaration> 
16.<Stmt> ::= ;

17.<ForStmt> ::= for ( <Expr> ; <OptExpr> ; <OptExpr> ) <Stmt>

18.<OptExpr> ::= <Expr>
19.<OptExpr> ::= ε

20.<WhileStmt> ::= while ( <Expr> ) <Stmt>

21.<IfStmt> ::= if ( <Expr> ) <Stmt> <ElsePart>

22.<ElsePart> ::= else <Stmt>
23.<ElsePart> ::= ε

24.<CompoundStmt> ::= { <StmtList> }

25.<StmtList> ::= <Stmt> <StmtList>
26.<StmtList> ::= ε

27.<Expr> ::= identifier = <Expr>
28.<Expr> ::= <Rvalue>

29.<Rvalue> ::= <Mag>
30.<Rvalue> ::= <Rvalue> <Compare> <Mag>

31.<Compare> ::= == 
32.<Compare> ::= <
33.<Compare> ::= >
34.<Compare> ::= <=
35.<Compare> ::= >=
36.<Compare> ::= !=

37.<Mag> ::= <Mag> + <Term> 
38.<Mag> ::= <Mag> - <Term>
39.<Mag> ::= <Term>

40.<Term> ::= <Term> * <Factor>
41.<Term> ::= <Term> / <Factor>
42.<Term> ::= <Factor>

43.<Factor> ::= ( <Expr> )
44.<Factor> ::= - <Factor>
45.<Factor> ::= + <Factor>
46.<Factor> ::= identifier
47.<Factor> ::= number